# Maintainer: Cedric Girard <girard.cedric@gmail.com>

pkgname=pidgin-libnotify-fork-git
pkgver=20110317
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="pidgin plugin that enables popups when someone logs in or messages you."
url="https://github.com/X-dark/ArchLinux/tree/master/code/pidgin-libnotify"
license=('GPL')
depends=('pidgin' 'libnotify' 'perlxml' 'gettext' 'notification-daemon')
makedepends=('libtool' 'intltool')
replaces=('gaim-libnotify' 'pidgin-libnotify')
options=(!libtool)

_gitroot="https://github.com/X-dark/pidgin-libnotify-fork.git"
_gitname="pidgin-libnotify-fork"

build() {
    cd "${srcdir}"
    msg "Connecting to GIT server...."

    if [ -d $_gitname ] ; then
        cd $_gitname && git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot $_gitname 
    fi  

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    rm -rf "${srcdir}/$_gitname-build"
    git clone "${srcdir}/$_gitname" "${srcdir}/$_gitname-build"
    cd "${srcdir}/$_gitname-build"
    
    ./autogen.sh
    ./configure --prefix=/usr --disable-deprecated  --disable-static
    make
}

package(){
    cd "${srcdir}/$_gitname-build"
    make DESTDIR="$pkgdir" install
}

