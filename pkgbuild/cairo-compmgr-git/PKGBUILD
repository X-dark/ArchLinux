# Maintainer: Cedric Girard <girard.cedric@gmail.com>
# Contributor: catalyst <catalyst@xaker.ru>
# Contributor: Jacob Winther <jacob.winther@gmail.com>

pkgname=cairo-compmgr-git
pkgver=20110317
pkgrel=1
pkgdesc="Cairo based composite manager - Git version"
arch=(i686 x86_64)
url="http://cairo-compmgr.tuxfamily.org/"
license=(GPL)
conflicts=(cairo-compmgr)
provides=(cairo-compmgr)
makedepends=(gettext git gtk-doc "intltool>=0.41" gconf)
depends=("gtk2>=2.16.0" "vala>=0.11" libsm)
options=(!makeflags
         !libtool)

_gitroot="git://git.tuxfamily.org/gitroot/ccm/cairocompmgr.git"
_gitname="cairocompmgr"

build() {
  cd "$srcdir"

  msg "Connecting to GIT server..."
  if [[ -d $_gitname ]]; then
    (cd $_gitname && git pull )
  else
    git clone $_gitroot $_gitname
  fi
  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf $_gitname-build
  cp -r $_gitname $_gitname-build
  cd $_gitname-build

  #patch for compatibility with vala 0.12
  sed -i 's!vala-0.10!libvala-0.12!' configure.ac
  sed -i 's!vala-0.10!libvala-0.12!' vapi/cairo-compmgr.deps

  ./autogen.sh --prefix=/usr

  make
}

package() {
  cd $_gitname-build

  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/usr/share/gconf/schemas/"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/cairo-compmgr.schemas" "$pkgdir/etc/gconf/schemas/*.schemas"
  rm -rf "$pkgdir/etc/gconf/schemas/"

}
