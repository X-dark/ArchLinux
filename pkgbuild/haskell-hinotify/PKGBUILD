# Maintainer: Cedric Girard <girard.cedric@gmail.com>
_hkgname=hinotify
pkgname=haskell-hinotify
pkgver=0.3.2
pkgrel=4
pkgdesc="Haskell binding to INotify"
url="http://hackage.haskell.org/package/${_hkgname}"
license=('custom:BSD3')
arch=('i686' 'x86_64')
makedepends=()
depends=('ghc' 'haskell-containers=0.5.0.0' 'haskell-directory=1.2.0.0' 'haskell-unix=2.6.0.0')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        https://github.com/kolmodin/hinotify/commit/{16db1dd1d8123d60a0c396c7fffbeb38c16430ff.diff,3277061d8305fc10f3f82a803d4607f9ff072162.diff})
install=${pkgname}.install
md5sums=('1105bc75012bd2a299181c47b21cac39'
         'af0631eced463d5891ff1abc129d9624'
         '2c4f45245d2f26af710361ff7222abc2')
build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    patch -p1 < ../16db1dd1d8123d60a0c396c7fffbeb38c16430ff.diff
    patch -p1 < ../3277061d8305fc10f3f82a803d4607f9ff072162.diff
    runhaskell Setup configure -O ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } --enable-split-objs --enable-shared \
       --prefix=/usr --docdir=/usr/share/doc/${pkgname} --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}
package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
