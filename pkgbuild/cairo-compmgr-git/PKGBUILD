# Maintainer: Cedric Girard <girard.cedric@gmail.com>
# Contributor: catalyst <catalyst@xaker.ru>
# Contributor: Jacob Winther <jacob.winther@gmail.com>

pkgname=cairo-compmgr-git
epoch=1
pkgver=0.3.1.35.g719a997
pkgrel=4
pkgdesc="Cairo based composite manager - Git version"
arch=(i686 x86_64)
url="http://cairo-compmgr.tuxfamily.org/"
license=(LGPL3)
conflicts=(cairo-compmgr)
provides=(cairo-compmgr)
makedepends=(gettext git gtk-doc "intltool>=0.41" gconf)
depends=("gtk2>=2.16.0" "vala>=0.26" libsm libgl)
options=(!makeflags
         !libtool)

source=('cairocompmgr::git+https://github.com/gandalfn/Cairo-Composite-Manager.git'
        'https://github.com/gandalfn/Cairo-Composite-Manager/pull/4.diff'
        'bfd_ansidecl.patch'
)
md5sums=('SKIP'
         '011074cd9687475ebf9ac93fc1f535aa'
         'b316779ffe9f86ada7ed601ae433789b')

pkgver() {
  cd "$srcdir"/cairocompmgr
  git describe --tags | sed 's|-|.|g'
}

prepare() {
  cd "$srcdir"/cairocompmgr

  #patch for compatibility with vala 0.26
  sed -i 's!libvala-0.18!libvala-0.26!' configure.ac
  sed -i 's!libvala-0.18!libvala-0.26!' vapi/cairo-compmgr.deps

  #patch needed to build with newer deps version
  patch -p1 < ../4.diff

  #bfd.h should include ansidecl.h
  patch -p2 < ../bfd_ansidecl.patch

}

build() {
  cd "$srcdir"/cairocompmgr

  ./autogen.sh --prefix=/usr LIBS="-ldl -lgmodule-2.0 -lm -lz"

  make
}

package() {
  cd "$srcdir"/cairocompmgr

  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/usr/share/gconf/schemas/"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/cairo-compmgr.schemas" "$pkgdir/etc/gconf/schemas/*.schemas"
  rm -rf "$pkgdir/etc/gconf/schemas/"

}

# vim:set ts=2 sw=2 et:
